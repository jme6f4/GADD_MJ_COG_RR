
TITLE: CANEVR_TRLBTS_1;

DATA:
	FILE = GD190618rr.csv;

VARIABLE:
	NAMES =
			ID_Fam   Wave     ID_MPL   
			DEMMAL   DEMAGE   DEMHSP   DEMRAC   
			SRPWCR  
			WASVCB   WASBLK   WASDGT   WSISUM   
			TRLATS   TRLBTS   
			CVLASF   CVLASC   CVLALF   CVLALC   
			CVLCOM   
			CANEVR   CANONS   CANFLF   
			CANFTY   CANF6M   CANAC6   
			CANYRS   
			ALCEVR   ALCONS   ALCFLF  
			ALCFTY   ALCF6M   ALCAC6  
			ALCYRS   
			SRPWCRDt WASVCBDt WASBLKDt 
			WASDGTDt WSISUMDt TRLATSDt 
			TRLBTSDt CVLASFDt CVLASCDt 
			CVLALFDt CVLALCDt CVLCOMDt   
		;
	USEVARIABLES =
		! outcome
	  		TRLBTS
  		! covariates
	  		DEMAGE
  			DEMMAL  		
  		! within predictor
  			CANEVR
  			ALCEVR
  		! between predictor
  			CANEVRBW
  			ALCEVRBW
  			;
  	USEOBSERVATION = 
  			(Wave EQ 1 AND TRLBTSDt EQ 1)
  		;
  	CLUSTER = 
  			ID_Fam
  		;
  	MISSING = .;
  	BETWEEN =   		
  			CANEVRBW
  			ALCEVRBW
  		;
  	WITHIN  		
  			CANEVR
  			ALCEVR
  			DEMAGE
  			DEMMAL
  		;  		

DEFINE: 
	! CREATE BW FAM PREDICTOR
		CANEVRBW = cluster_mean (CANEVR);	
		ALCEVRBW = cluster_mean (ALCEVR);	
	! CENTER WI FAM VARIABLES
		CENTER CANEVR (GROUPMEAN);
		CENTER ALCEVR (GROUPMEAN);
		CENTER DEMMAL (GRANDMEAN);
		CENTER DEMAGE (GRANDMEAN);		
	! CENTER BW FAM VARIABLES
		CENTER CANEVRBW (GRANDMEAN);
		CENTER ALCEVRBW (GRANDMEAN);
    
ANALYSIS:
        ITERATIONS = 1000000;
        TYPE = TWOLEVEL RANDOM;
        ALGORITHM = INTEGRATION;
        
OUTPUT:
	SAMPSTAT STDYX TECH1 CINT;

MODEL:
%WITHIN%
	TRLBTS on CANEVR;
	TRLBTS on  DEMMAL DEMAGE ALCEVR;	
	TRLBTS*;
	CANEVR*;

	CANEVR WITH DEMMAL*;
	CANEVR WITH DEMAGE*;
	ALCEVR WITH DEMMAL*;
	ALCEVR WITH DEMAGE*;
	CANEVR WITH ALCEVR*;

%BETWEEN%
	TRLBTS on CANEVRBW ALCEVRBW;
	TRLBTS*;
	CANEVRBW*;
	CANEVRBW WITH ALCEVRBW*;
