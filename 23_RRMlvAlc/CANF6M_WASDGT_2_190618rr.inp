
TITLE: CANF6M_WASDGT_2;

DATA:
	FILE = GD190618rr.csv;

VARIABLE:
	NAMES =
			ID_Fam   Wave     ID_MPL   
			DEMMAL   DEMAGE   DEMHSP   DEMRAC   
			SRPWCR  
			WASVCB   WASBLK   WASDGT   WSISUM   
			TRLATS   TRLBTS   
			CVLASF   CVLASC   CVLALF   CVLALC   
			CVLCOM   
			CANEVR   CANONS   CANFLF   
			CANFTY   CANF6M   CANAC6   
			CANYRS   
			ALCEVR   ALCONS   ALCFLF  
			ALCFTY   ALCF6M   ALCAC6  
			ALCYRS   
			SRPWCRDt WASVCBDt WASBLKDt 
			WASDGTDt WSISUMDt TRLATSDt 
			TRLBTSDt CVLASFDt CVLASCDt 
			CVLALFDt CVLALCDt CVLCOMDt   
		;
	USEVARIABLES =
		! outcome
	  		WASDGT
  		! covariates
	  		DEMAGE
  			DEMMAL  		
  		! within predictor
  			CANF6M
  			ALCF6M
  		! between predictor
  			CANF6MBW
  			ALCF6MBW
  			;
  	USEOBSERVATION = 
  			(Wave EQ 2 AND WASDGTDt EQ 1)
  		;
  	CLUSTER = 
  			ID_Fam
  		;
  	MISSING = .;
  	BETWEEN =   		
  			CANF6MBW
  			ALCF6MBW
  		;
  	WITHIN  		
  			CANF6M
  			ALCF6M
  			DEMAGE
  			DEMMAL
  		;  		

DEFINE: 
	! CREATE BW FAM PREDICTOR
		CANF6MBW = cluster_mean (CANF6M);	
		ALCF6MBW = cluster_mean (ALCF6M);	
	! CENTER WI FAM VARIABLES
		CENTER CANF6M (GROUPMEAN);
		CENTER ALCF6M (GROUPMEAN);
		CENTER DEMMAL (GRANDMEAN);
		CENTER DEMAGE (GRANDMEAN);		
	! CENTER BW FAM VARIABLES
		CENTER CANF6MBW (GRANDMEAN);
		CENTER ALCF6MBW (GRANDMEAN);
    
ANALYSIS:
        ITERATIONS = 1000000;
        TYPE = TWOLEVEL RANDOM;
        ALGORITHM = INTEGRATION;
        
OUTPUT:
	SAMPSTAT STDYX TECH1 CINT;

MODEL:
%WITHIN%
	WASDGT on CANF6M;
	WASDGT on  DEMMAL DEMAGE ALCF6M;	
	WASDGT*;
	CANF6M*;

	CANF6M WITH DEMMAL*;
	CANF6M WITH DEMAGE*;
	ALCF6M WITH DEMMAL*;
	ALCF6M WITH DEMAGE*;
	CANF6M WITH ALCF6M*;

%BETWEEN%
	WASDGT on CANF6MBW ALCF6MBW;
	WASDGT*;
	CANF6MBW*;
	CANF6MBW WITH ALCF6MBW*;
